

                ;	MDISK  Relocatable M-Drive/H Driver module
                ;
                ; ASM MDISK (ORG 0000h)
                ; DDT
                ; IMDISK.HEX
                ; R100
                ; G0
                ; ERA MDISK000.COM
                ; SAVE 1 MDISK000.COM
                ; ASM MDISK (ORG 0100h)
                ; LOAD MDISK
                ; ERA MDISK100.COM
                ; REN MDISK100.COM=MDISK.COM
                
                
 0000           	ORG	0000h
                ;	ORG	0100h
                
                ;
                ;	M - D R I V E / H
                ;
                ;	The DPB is set for one M-DRIVE/H board, and will be adjusted by the
                ;	installer progam accordingly.
                ;	Information such as sector, track, and DMA address are saved even
                ;	if they are not intended for the memory drive.	The performance of
                ;	any CP/M BIOS can be greatly enhanced by integrating this code
                ;	directly into an existing BIOS, as well as save some memory space.
                ;
                ;	This code expects the M-DRIVE/H board to be addressed at the
                ;	following I/O ports:
                ;
                ;	0C6H - Data
                ;	0C7H - Address
                ;
                ;		Written L. Ott	12/17/82
                ;		1982 by CompuPro div. Godbout Electronics
                ;
                ;	Updated for use by self-installer by Michael Enkelis 2023
                ;	Self-installer written by Michael Enkelis with some code
                ;	written by Mike Douglas 2017.
                
 00C6 =         HBASE	EQU	0C6h
 00C6 =         HDATA	EQU	HBASE
 00C7 =         HADDR	EQU	HBASE+1
                
 004D =         MEMDRV	EQU	'M'				;SET AS DRIVE "M"
 0008 =         SPT		equ	8				;Sectors per track
 0080 =         secsize	equ	128				;Size of sector in bytes
 0000 =         NOTHING	equ	0
                
 0010 =         SIB:	EQU	2048/secsize	;SECTORS IN MEMORY DRIVE DATA BLOCK
 00FE =         DSM:	EQU	((512-4)*SPT)/SIB
                ;
 0000 C36100    MDRIVE:	JMP		HOME		;HOME
 0003 C35200    		JMP		SELDSK		;SELECT DISK
 0006 C36A00    		JMP		SETTRK		;SET TRACK
 0009 C37200    		JMP		SETSEC		;SET SECTOR
 000C C37900    		JMP		SETDMA		;SET DMA
 000F C38900    		JMP		READ		;READ
 0012 C39300    		JMP		WRITE		;WRITE
 0015 C38100    		JMP		SECTRN		;SECT0R TRANSLATE
                ;
                ; Following filled in by installer.
 0018 C30000    BIOSHOM:JMP		NOTHING		;HOME
 001B C30000    BIOSSEL:JMP		NOTHING		;SELECT DISK
 001E C30000    BIOSTRK:JMP		NOTHING		;SET TRACK
 0021 C30000    BIOSSEC:JMP		NOTHING		;SET SECTOR
 0024 C30000    BIOSDMA:JMP		NOTHING		;SET DMA
 0027 C30000    BIOSRED:JMP		NOTHING		;READ
 002A C30000    BIOSWRT:JMP		NOTHING		;WRITE
 002D C30000    BIOSTRN:JMP		NOTHING		;SECT0R TRANSLATE
                
                
                ;
                ;	Driver variables used (set) by loader.
                ;
 0030 E600      DRIVE:	DW		MDISKL		;Read by loader. DO NOT CHANGE VALUE OR LOCATION
                							;Filled in by loader with DRIVE (MEMDRV-'A') code
                							;Filled in by loader with M-DRIVE/H TYPE flag
                
                ;
                ;	Disk Parameter Block
                ;
                MD$DPB:						;SET FOR 1 BOARD, 8 SPT
 0032 0800      		DW	SPT				;SPT (See NOTE)
 0034 040F      		DB	4,15			;BSH, BLH
 0036 01        		DB	1				;EXM (See NOTE)
 0037 FD00      		DW	DSM-1			;DSM (See NOTE)
 0039 7F00      		DW	128-1			;DRM
 003B C000      		DB	0C0h,00h		;AL0, AL1 (RESERVED DIR BLOCKS, MUST MATCH ...
                							; ... NUMBER OF DIRECTORY ENTRIES)
 003D 0000      		DW	0				;CKS (CHECK SUM), This is always zero.
 003F 0400      		DW	4				;OFF (NUMBER OF RESERVED TRAGKS FOR PARITY CHECK
                							;	THIS IS ALWAYS 4)
                ;
                ; NOTE: SPT, EXM, DSM --- WILL BE CHANGED FOR MORE BOARDS BY LOADER.
                
 0041 00        SELFLG:	DB		0			;M-DRIVE/H selected flag. DO NOT CHANGE LOCATION
                							;0 = Not selected, -1 Selected
                ;
                ;	16 byte Disk Parameter Header
                ;
 0042 0000      MD$DPH:	DW	0				;SECT0R TRANSLATE TABLE (NONE)
 0044 0000000000		DW	0,0,0			;SCRATCH AREA
 004A EB00      		DW	BUFDIR			;DIRECT0RY BUFFER
 004C 3200      		DW	MD$DPB			;DPB BLOCK
 004E 0000      		DW	0				;CHECKSUM VECTOR
 0050 6B01      		DW	ALV				;ALL0CATI0N VECTOR
                
                ;
                ;	S E L E C T   D I S K   D RI V E
                
                ;	Select the disk drive for subsequent disk transfers and return
                ;	the address of the DPH for the memory drive.
                ;	ENTRY -- <C> = disk selection value.
                ;
                ;	EXIT -- <HL> = DPH address of the memory drive.
                ;	        If memory drive not selected, control is passed to BIOS
                ;			Return <HL> = 0 if invalid drive selected.
                
 0052 214100    SELDSK:	LXI		H,SELFLG	;<HL> = Selected flag
 0055 3600      		MVI		M,0			;M=0, SET MEMORY DRIVE AS NOT SELECTED
 0057 3A3000    		LDA		DRIVE		;<A>=DRIVE code
 005A B9        		CMP		C			;IS MEMORY DRIVE SELECTED?
 005B C21B00    		JNZ		BIOSSEL		;IF NOT GO BACK TO BIOS
 005E 35        		DCR		M			;M=-1, SET MEMORY DRIVE AS SELECTED
 005F 23        		INX		H			;<HL> = DPH ADDRESS
 0060 C9        		RET					;AND DONE
                ;
                ;	H O M E
                ;	Set current track to 0
                
 0061 210000    HOME:	LXI		H,0
 0064 22E700    		SHLD	TRACK		;SET TRACK AS 0
 0067 C31800    		JMP		BIOSHOM		;ON TO BIOS
                ;
                ;	S  E  T   T R A C K
                ;
                ;	Set track number.  The track number 1s save for later use during
                ;	a disk transfer.  The track is save whether or not the memory disk
                ;	is currently selected.
                ;
                ;	ENTRY -- <BC> = Track number, 0 to 65535.
                
 006A 69        SETTRK:	MOV		L,C
 006B 60        		MOV		H,B
 006C 22E700    		SHLD	TRACK
 006F C31E00    		JMP		BIOSTRK		;ON TO BIOS
                ;
                ;	S E T	S E C T 0 R
                ;
                ;	Set	sector number.  The sector number is save for later use during
                ;	a disk transfer.  The sector is saved whether or not the memory
                ;	disk is current1y selected.
                ;
                ;	ENTRY -- <BC> = Sector number, 0 to 255.
                
 0072 79        SETSEC:	MOV		A,C
 0073 32E600    		STA		SECTOR		;Set sector
 0076 C32100    		JMP		BIOSSEC		;ON TO BIOS
                ;
                ;	S E T   D M A   A D D R E S S
                ;
                ;	Set the address for the next DMA transfer.
                ;	The DMA address is saved for later use during a disk transfer.
                ;	The address is saved whether or not the memory disk ia
                ;	currently selected.
                ;
                ;	ENTRY -- <BC> = Disk memory address.
                
 0079 69        SETDMA:	MOV		L,C
 007A 60        		MOV		H,B
 007B 22E900    		SHLD	DMAADR		;Set DMA
 007E C32400    		JMP		BIOSDMA		;ON TO BIOS
                ;
                ;	S  E  C  T O R   T R A N S L A T E
                ;
                ;	Translate the sector number from logical to physical.
                ;
                ;	ENTRY	<DE> = 0, no translatlon required.
                ;           <DE> = Translation table address.
                ;           <BC> = Number to translate.
                ;
                ;	EXIT	<HL> = Physical sector number.
                
 0081 60        SECTRN:	MOV		H,B
 0082 69        		MOV		L,C			;HL=Physical sector number
 0083 7A        		MOV		A,D			;DE=Translation table address
 0084 B3        		ORA		E			;Are we using translation?
 0085 C8        		RZ					;No, All done
 0086 C32D00    		JMP		BIOSTRN		;Yes, Let BIOS translate
                
                ;
                ;	R E A D
                ;
                ;	Read the currently selected track,sector from the currently
                ;	selected drive.
                
 0089 3A4100    READ:	LDA		SELFLG		;SELFLG= -1 if selected
 008C A7        		ANA		A			;IS MEMORY DRIVE SELECTED
 008D CA2700    		JZ		BIOSRED		;ON TO BIOS
 0090 C39B00    		JMP		MDFNL		;A= -1 for READ
                ;
                ;	W R I T E
                ;
                ;	Write to the current1y selected drive at the current track
                ;	and sector.
                
 0093 3A4100    WRITE:	LDA		SELFLG		;SELFLG= -1 if selected
 0096 A7        		ANA		A			;IS MEMORY DRIVE SELECTED
 0097 CA2A00    		JZ		BIOSWRT		;ON TO BIOS
 009A AF        		XRA		A			;A= 0 for WRITE
                
                ;	Fall into MDFNL
                ;
                ;	MDFNL - Hard memory disk final command processing
                ;	ENTRY: <A> = Command. -1 = READ, 0 = WRITE
                
 009B 47        MDFNL:	MOV		B,A			;Save command
 009C 2AE700    		LHLD	TRACK		;GET TRACK
 009F 54        		MOV		D,H			;
 00A0 5D        		MOV		E,L			;Save untranslated TRACK
 00A1 29        		DAD		H			;*2
 00A2 29        		DAD		H			;*4
 00A3 29        		DAD		H			;*8
 00A4 29        		DAD		H			;*l6
 00A5 29        		DAD		H			;*32
 00A6 29        		DAD		H			;*64
 00A7 29        		DAD		H			;*128
 00A8 CDDA00    		CALL	SETMDRV		;SET M-DRIVE/H BOARD VIA <HL>
 00AB 2AE900    		LHLD	DMAADR		;DIRECTLY TO CPM BUFFER
 00AE 0E80      		MVI		C,secsize	;<C> = 128 BYTE'S PER SECTOR
 00B0 04        		INR		B			;<B>=0 READ [Z=1], 1 WRITE [Z=0]
 00B1 41        		MOV		B,C			;Set <B> for parity check
 00B2 C2C700    		JNZ		M$WRITE		;IF WRITE COMMAND JUMP
                
                ;		Fall into M$READ
                ;
                ;	GET A 128 BYTE BLOCK FROM M-DRIVE/H BOARD
                ;	ADDRESS IS ALREADY SELECTED.
                ;	ENTRY -- <C> IS NUMBER OF BYTES TO MOVE
                ;			 <HL> IS DMA BUFFER TO PUT DATA
                ;
                ;	EXIT -- <A> = 0 No errors, > 0 READ error.
                
 00B5 DBC6      M$READ:	IN		HDATA		;GET DATA BYTE
 00B7 77        		MOV		M,A			;PUT TO DMA BLOCK
 00B8 23        		INX		H			;UP POINTER
 00B9 80        		ADD		B			;ADD UP PARITY BYTE
 00BA 47        		MOV		B,A			; AND SAVE
 00BB 0D        		DCR		C			;C0UNT THIS MOVE
 00BC C2B500    		JNZ		M$READ		;AND DO AGAIN
 00BF EB        		XCHG				;<HL> = untranslated TRACK
 00C0 CDDA00    		CALL	SETMDRV		;SELECT PARITY BYTE LOCATION VIA <HL>
 00C3 DBC6      		IN		HDATA		;GET PARITY BYTE
 00C5 90        		SUB		B			; 0 IF NO ERROR
 00C6 C9        		RET					;	THEN RETURN
                ;
                ;	PUT A BLOCK OF DATA INTO M-DRIVE/H BOARD
                ;	ADDRESS IS ALREADY SELECTED.
                ;	ENTRY -- <C>  IS NUNBER OF BYTES TO MOVE
                ;			 <HL> IS DMA BUFFER TO GET DATA FROM
                ;
                ;	EXIT -- <A> = 0 No errors.
                
 00C7 7E        M$WRITE:MOV		A,M			;GET BYTE FROH BUFFER
 00C8 23        		INX		H			;UP POINTER
 00C9 D3C6      		OUT		HDATA		;TO MDRIVE BOARD
 00CB 80        		ADD		B			;ADD UP PARITY BYTE
 00CC 47        		MOV		B,A			; AND SAVE
 00CD 0D        		DCR		C			;COUNT THIS MOVE
 00CE C2C700    		JNZ		M$WRITE		;AND DO AGAIN
 00D1 EB        		XCHG				;<HL> = untranslated TRACK
 00D2 CDDA00    		CALL	SETMDRV		;SELECT PARITY BYTE LOCATION
 00D5 78        		MOV		A,B
 00D6 D3C6      		OUT		HDATA		;WRITE PARITY BYTE TO HEMORY DISK
 00D8 AF        		XRA		A			;AND SHOW NO ERROR
 00D9 C9        		RET					;RETURN
                ;
                ;	Set	M-DRIVE/H board to address in <HL>
                ;	The combined values of <AHL> is a 24 bit number
                ;	which is three more bits for a 2 Mbyte card, and
                ;	five more bits than a 512 Kbyte card.
                
 00DA 3AE600    SETMDRV:LDA		SECTOR		;Get sector 0-31 (0x1F)
 00DD D3C7      		OUT		HADDR		;<A>: to M-DRIVE/H board
 00DF 7C        		MOV		A,H			;<H>: is always 0 or 1
 00E0 D3C7      		OUT		HADDR
 00E2 7D        		MOV		A,L			;<L>: Upper bit's picks track 0-3
 00E3 D3C7      		OUT		HADDR		; remainder picks sector and byte
 00E5 C9        		RET
                
 00E6 =         MDISKL:	EQU		$-MDRIVE	;LENGTH OF MDRIVE BIOS
                ;	End of code
                
                ;
                ;	Flags and Buffers
                ;
                
 00E6           SECTOR:	DS		1			;SECTOR: 0-31
 00E7           TRACK:	DS		2			;TRACK:  0-65535
 00E9           DMAADR:	DS		2			;ADDRESS OF DMA BUFFER
 00EB           BUFDIR:	DS		128			;DIRECTORY BUFFER
 016B           ALV:	DS		(((DSM*SPT)+7)/SPT)+1
                
 026A           	END
